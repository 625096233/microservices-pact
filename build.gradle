buildscript {
    ext {
        springBootVersion = '1.2.3.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }

    }
    dependencies {
        classpath('com.github.jengelman.gradle.plugins:gradle-processes:0.3.0')
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'au.com.dius:pact-jvm-provider-gradle_2.11:2.1.13'
    }
}



allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
}

subprojects {
    apply plugin: 'spring-boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("au.com.dius:pact-jvm-consumer-junit_2.11:2.1.13")
    }
}

project(':microservices-pact-consumer') {
    jar {
        baseName = 'microservices-pact-consumer'
        version = '0.0.1-SNAPSHOT'
    }
}


project(':microservices-pact-provider') {
    apply plugin: 'au.com.dius.pact'
    apply plugin: 'com.github.johnrengelman.processes'

    task startProvider(type: com.github.jengelman.gradle.plugins.processes.tasks.JavaFork) {
      classpath = files('microservices-pact-provider/build/libs/microservices-pact-provider-0.0.1-SNAPSHOT.jar')
      classpath += sourceSets.main.runtimeClasspath
      main = 'io.pivotal.microservices.pact.provider.Application'
      doLast {
        Thread.sleep(5000)
      }
    }

    task stopProvider << {
      startProvider.processHandle.abort()
    }

    jar {
        baseName = 'microservices-pact-provider'
        version = '0.0.1-SNAPSHOT'
    }

    pact {
        serviceProviders {
            fooProvider {
                startProviderTask = 'startProvider'
                terminateProviderTask = 'stopProvider'
                hasPactWith('fooConsumer') {
                    pactFile = file(System.getenv("PACT_FILE"))
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
